//Android项目开发插件
apply plugin: 'com.android.application'
//kotlin开发插件
apply plugin: 'kotlin-android'
//kotlin注解处理插件，kapt是kotlin的注解处理器，kapt 也能够处理 Java 文件，所以需要替换 annotationProcessor 关键字
apply plugin: 'kotlin-kapt'
//应用Hilt插件 ,Hilt是 Android 的依赖项注入库，可减少在项目中执行手动依赖项注入的样板代码。
apply plugin: 'dagger.hilt.android.plugin'
//导航的插件,safeargs用于在fragment之间跳转传输少量数据,适用于 Java 模块或 Java 和 Kotlin 混合模块的 Java 语言代码
apply plugin: "androidx.navigation.safeargs"
android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.sq26.experience"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                //把Room在编译时将数据库的架构信息导出到JSON文件中。
                // 在room.schemaLocation中设置导出路径
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
    }
    //视图绑定功能启用
    buildFeatures {
        viewBinding true
        dataBinding true
    }
    buildTypes {
        release {
            // 是否进行混淆
            minifyEnabled true
            // 混淆文件的位置，其中'proguard-android.txt'为sdk默认的混淆配置，
            //'proguard-rules.pro' 是该模块下的混淆配置
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    //注解框架需要的支持
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
//        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
    }
    buildToolsVersion '30.0.3'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.4.21"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    //Google团队开发的Material风格的新组件支持库
    implementation 'com.google.android.material:material:1.2.1'
    //constraintLayout布局控件
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    //RecyclerView控件
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    //CardView控件,纸片布局
    implementation 'androidx.cardview:cardview:1.0.0'
    //新的viewPager2控件,找机会试试
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    //Butter Knife,黄油刀,view注解框架
    implementation 'com.jakewharton:butterknife:10.2.3'
    kapt 'com.jakewharton:butterknife-compiler:10.2.3'
    //fastJson json解析库
    implementation 'com.alibaba:fastjson:1.2.74'
    //RxJava链式异步处理库
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.11'
    //zxing框架核心
    implementation 'com.google.zxing:core:3.4.1'
    //Fresco框架
    implementation 'com.facebook.fresco:fresco:2.2.0'
    //获取图片的属性标签
    implementation 'androidx.exifinterface:exifinterface:1.3.2'
    //okHttp轻量级网络框架
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    //android文件管理类
    implementation 'androidx.documentfile:documentfile:1.0.1'
    //AndroidPdfViewer 查看pdf的控件
    implementation 'com.github.barteksc:android-pdf-viewer:2.8.2'
    //Android的原始Socket，HTTP（s）客户端/服务器和WebSocket库
    implementation 'com.koushikdutta.async:androidasync:3.0.9'
    //生命周期感知型组件
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.2.0"
    // Room 组件(数据库框架)
    //Android官网依赖是这样的，java开发人员使用
//    implementation "android.arch.persistence.room:runtime:$room_version"
//    annotationProcessor 'android.arch.persistence.room:compiler:$room_version'
    //对于那些使用Kotlin的人，请尝试在应用中更改annotationProcessor为kapt
//    implementation "android.arch.persistence.room:runtime:$room_version"
//    kapt "android.arch.persistence.room:compiler:$room_version"
//如果您已迁移到androidx
    implementation "androidx.room:room-runtime:2.2.6"
    kapt "androidx.room:room-compiler:2.2.6"
    //Kotlin扩展程序和协同程序对Room的支持
    implementation "androidx.room:room-ktx:2.2.6"
    // paging 组件(数据分页)
    implementation "androidx.paging:paging-runtime-ktx:2.1.2"
    //navigation组件(fragment管理)
    implementation "androidx.navigation:navigation-runtime-ktx:2.3.2"
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.2"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.2"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:2.3.2"
    implementation "androidx.navigation:navigation-compose:1.0.0-alpha04"
    //activity的kotlin强化库
    implementation "androidx.activity:activity-ktx:1.1.0"
    //Fragment KTX 提供了一系列扩展程序以简化 Fragment API。
    implementation "androidx.fragment:fragment-ktx:1.2.5"
    //Android 上的 Kotlin 协程
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2'
    //Android ktx,KTX 扩展程序可以为 Jetpack、Android 平台及其他 API 提供简洁的惯用 Kotlin 代码。
    implementation "androidx.core:core-ktx:1.3.2"
    //Collection 扩展程序包含在 Android 的节省内存的集合库中使用的实用函数，包括 ArrayMap、LongParseArray、LruCache 等等。
    implementation "androidx.collection:collection-ktx:1.1.0"
    //ViewModel KTX 库提供了一个 viewModelScope() 函数，可让您更轻松地从 ViewModel 启动协程。
    // CoroutineScope 绑定至 Dispatchers.Main，并且会在清除 ViewModel 后自动取消。
    // 您可以使用 viewModelScope()，而无需为每个 ViewModel 创建一个新范围。
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    //dagger hilt 解耦各个模块之间的强关联性
    implementation "com.google.dagger:hilt-android:2.28.3-alpha"
    kapt "com.google.dagger:hilt-android-compiler:2.28.3-alpha"
    //hilt,Viewmodel生命周期支持
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha02"
}
